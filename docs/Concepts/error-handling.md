---
id: error-handling
title: Error handling
---

Error handling is the process of anticipating and responding to errors that might occur in your application. 4D provides a comprehensive support for catching and reporting errors at runtime, as well as for investigating their conditions. 

Error handling meets two main needs:

- finding out and fixing potential errors and bugs in your code during the development phase,
- catching and recovering from unexpected errors in deployed applications; in particular, you can replace system error dialogs (disk full, missing file, etc.) with you own interface. 

## Installing an error-handling method

In 4D, all errors can be catched and handled in a specific project method, the **error-handling** (or **error-catching**) method.

This project method is installed for the current process and will be automatically called for any error that occurs in the process, in interpreted or compiled mode. To *install* this project method, you just need to call the `ON ERR CALL` command with the project method name as parameter. For example:

```
ON ERROR CALL("IO_ERRORS") //Installs the error-handling method
```

To stop catching errors and give back hand to 4D, call `ON ERR CALL` with an empty string:
```
ON ERR CALL("") //gives back control to 4D
```

### Scope and components

You can define a single error-catching method for the whole application or different methods per application module. However, only one method can be installed per process.

An error-handling method installed by the `ON ERR CALL` command only applies to the running database. In the case of an error generated by a **component**, the `ON ERR CALL` error-handling method of the host database is not called, and vice versa. 

The  `Method called on error` command allows to know the name of the method installed by `ON ERR CALL` for the current process. It is particularly useful in the context of components because it enables you to temporarily change and then restore the host database error-catching method:

```
 $methCurrent:=Method called on error
 ON ERR CALL("NewMethod")
  //If the document cannot be opened, an error is generated
 $ref:=Open document("MyDocument")
  //Reinstallation of previous method
 ON ERR CALL($methCurrent)

```

### Handling errors within the method

Within the custom error method, you have access to several information that will help you identifying the error:

- dedicated [system variables](Concepts/system-variables.md):

  - `Error` (longint): error code
  - `Error method` (text): name of the method that triggered the error
  - `Error line` (longint): line number in the method that triggered the error
  - `Error formula` (text): formula of the 4D code (raw text) which is at the origin of the error. 
  
- the `GET LAST ERROR STACK` command that returns information about the current stack of errors of the 4D application. 


#### Example

Here is a simple error-handling system:

```
//installing the error handling merthod
 ON ERR CALL("errorMethod")
 //... executing code
 ON ERR CALL("") //giving control back to 4D
```

```
// errorMethod project method
 If(Error#1006) //this is not a user interruption
    ALERT("The error "+String(Error)+" occurred. The code in question is: \""+Error formula+"\"")
 End if
```

### Using an empty error-handling method

If you mainly want the standard error dialog box to be hidden, you can install an empty error-handling method. The `Error` system variable can be tested in any method, i.e. outside of the error-handling method:

```
ON ERR CALL("emptyMethod") //emptyMethod exists but is empty
$doc:=Open document( "myFile.txt")
If (error = -43)
	ALERT("File not found.")
End if
ON ERR CALL("")

```


## Investigating errors

Errors often happen in specific contexts and/or following a particular sequence of events. Getting and analyzing detailed log files is usually an efficient way to understand and fix those errors.

4D provides several log files:

- **4DRequestsLog.txt**: records standard requests carried out by the 4D Server machine or the 4D remote machine that executed the command (excluding Web requests). 
- **4DRequestsLog_ProcessInfo.txt**: records information on each process created on the 4D Server machine or the 4D remote machine that executed the command (excluding Web requests).
- **4DDebugLog.txt**: records each event occurring at the 4D programming level.
- **HTTPDebugLog.txt**: records each HTTP request and each response in raw mode.

All log files share the same sequential operation numbers so that each log entry can be linked to other entries in other logs. 

Log files can be enabled and disabled at any moment using the `SET DATABASE PARAMETER` or `WEB SET OPTION` commands. 
